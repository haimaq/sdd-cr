Option Explicit


'Convert a Dictionary to a json object
'If the dictionary is empty or contain only numeric keys, the json object will be an array
Function jsonFromObject(dico)
	Dim json, key, isFirst, isArray
	isArray = true
	For Each key in dico
		If IsEmpty(key) Or Not IsNumeric(key) Then
			isArray = false
			Exit For
		End If
	Next
	If isArray Then
		json = "["
	Else
		json = "{"
	End If
	isFirst = true
	For Each key in dico
		If isFirst Then
			isFirst = false
		Else 
			json = json & ","
		End If
		If isArray Then
			json = json & jsonValOnly(dico.Item(key))
		Else
			json = json & jsonKeyVal(key, dico.Item(key))
		End If
	Next
	If isArray Then
		json = json & "]"
	Else
		json = json & "}"
	End If
	jsonFromObject = json
End Function

'Get a json "key:value"
Function jsonKeyVal(param,value)
	jsonKeyVal = "" & chr(34) & param & chr(34) & ":" & jsonValOnly(value)
End Function

'Get a json "value", without any "key:" before
Function jsonValOnly(value)
	Select Case VarType(value)
		Case 2,3,4,5,17 'Interger, Long, Single, Double, Byte
			jsonValOnly = value
		Case 8,11 ' String, Boolean
			jsonValOnly = chr(34) & jsonEncoding(value) & chr(34)
		Case 9 'Object (Array not supported by AppScript)
			jsonValOnly = jsonFromObject(value)
		Case 0,1
			jsonValOnly = chr(34) & chr(34)
		Case Else 'otherwise
			jsonValOnly = chr(34) & "Not supported data type" & chr(34)
	End Select
End Function

'Convert a String to a compatible json String
Function jsonEncoding(txt)
	Dim result
	result = txt
	result = Replace(result, "\","\\") 'to do first !!!
	result = Replace(result, chr(13) & chr(10), "\n")
	result = Replace(result, chr(13), "\n")
	result = Replace(result, chr(10), "\n")
	result = Replace(result, chr(9), "\t")
	'result = Replace(result,"'","\'")
	result = Replace(result,chr(34),"\" & chr(34))
	jsonEncoding = result
End Function



Sub GetUsers(findMe)  
	Dim lstRec,rec,json,whereClause, fldValue
	'whereClause = "TS_TITLE  like %" & CStr(findMe) & "%"
	Set lstRec = Ext.CreateAppRecordList(Ext.TableId("USR_CRS_CHANGEREQ_TAGS"))
	Call lstRec.ReadWithWhere("TS_TITLE  like %" & CStr(findMe) & "%")
	'Call DoLogging(Time&" Nb DB found: " & CStr(lstRec.Count()) & vbcrlf,0)
	Set json=CreateObject("Scripting.Dictionary")
	For Each rec in lstRec
		json.Add json.Count, CreateObject("Scripting.Dictionary")
		json.Item(json.Count-1).Add "id", rec.GetId()
		Call rec.GetFieldValue("TITLE", fldValue) 
		json.Item(json.Count-1).Add "title", fldValue
	Next
	Call Ext.WriteStream(lstRec.Count)
	json = jsonFromObject(json)
	Call Ext.WriteStream(json)
End Sub





Dim find
find = Shell.Params.Item("find")
Call Ext.WriteStream(find)
Call GetUsers(find)
